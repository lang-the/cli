/*!
 * Copyright (c) Aaron Delasy
 * Licensed under the MIT License
 */

import zip from "the/zip"

export fn request (url: str, path: str) buffer_Buffer {
  archivePath := path + path_SEP + "file.zip"
  zip(archivePath, "*", cwd: path)
  data := fs_readFileSync(archivePath)

  headers := [
    request_Header{name: "authorization", value: "pELerETRaVeYHEne"},
    request_Header{name: "content-type", value: "application/zip"}
  ]

  mut req := request_open("POST", url, headers: headers, data: data)
  res := req.read()
  req.close()

  fs_rmSync(archivePath)

  if res.status != 200 {
    print(res.data.str(), to: "stderr")
    process_exit(1)
  }

  return res.data
}

export fn customCompile (compiler: str, action: str, filePath: str, outputPath: str, platform: str, arch: str) buffer_Buffer {
  isCompile := action == "compile"
  actionArg := isCompile ? "" : " " + action
  quote := os_NAME == "Windows" ? "\"" : "'"
  fileArg := " " + quote + filePath + quote

  mut optionsArg := ""

  if !outputPath.empty {
    optionsArg += " " + quote + "--output=" + outputPath + quote
  }

  if !platform.empty && platform != os_NAME.lower {
    optionsArg += " " + quote + "--platform=" + platform + quote
  }

  if !arch.empty && arch != os_ARCH {
    optionsArg += " " + quote + "--arch=" + arch + quote
  }

  stdout := process_runSync(compiler + actionArg + optionsArg + fileArg)
  return stdout
}
