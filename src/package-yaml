/*!
 * Copyright (c) Aaron Delasy
 * Licensed under the MIT License
 */

import * as YAML from "the/yaml"

export obj PackageYAML {
  mut name: str
  mut version: str
  mut description: str
  mut license: str
  mut mainFile: str
  mut files: str[]
  mut packages: str[str]
  mut scripts: str[str]

  fn stringify (self: ref Self) str {
    mut code := ""

    if !self.name.empty {
      code += "name: " + self.name + os_EOL
    }

    if !self.version.empty {
      code += "version: " + self.version + os_EOL
    }

    if !self.description.empty {
      code += "description: " + self.description + os_EOL
    }

    if !self.license.empty {
      code += "license: " + self.license + os_EOL
    }

    if !self.mainFile.empty {
      code += "main: " + self.mainFile + os_EOL
    }

    if !self.files.empty {
      code += code.empty ? "" : os_EOL
      code += "files:" + os_EOL

      loop i := 0; i < self.files.len; i++ {
        code += "  - " + self.files[i] + os_EOL
      }
    }

    if !self.packages.empty {
      code += code.empty ? "" : os_EOL
      code += "packages:" + os_EOL
      mut keys := self.packages.keys
      keys.sort((a: str, b: str) -> int { return a > b })

      loop i := 0; i < keys.len; i++ {
        key := keys[i]
        code += "  " + key + ": " + self.packages.get(key) + os_EOL
      }
    }

    if !self.scripts.empty {
      code += code.empty ? "" : os_EOL
      code += "scripts:" + os_EOL
      mut keys := self.scripts.keys
      keys.sort((a: str, b: str) -> int { return a > b })

      loop i := 0; i < keys.len; i++ {
        key := keys[i]
        code += "  " + key + ": " + self.scripts.get(key) + os_EOL
      }
    }

    return code
  }
}

export fn PackageYAML_parse (content: str) PackageYAML {
  mut yaml := YAML.parse(content)
  mut result := PackageYAML{}

  loop yaml.hasNext() {
    node := yaml.next()

    if node is YAML.NodeMapping {
      mappingNode := node
      nodeKey := YAML.Node_stringify(mappingNode.key)

      if nodeKey == "name" {
        if mappingNode.value != nil {
          val := mappingNode.value
          if val is YAML.NodeScalar {
            result.name = YAML.Node_stringify(val)
          } else {
            throw error_NewError("Property `name` of package.yml expected to be a scalar")
          }
        }
      } elif nodeKey == "version" {
        if mappingNode.value != nil {
          val := mappingNode.value
          if val is YAML.NodeScalar {
            result.version = YAML.Node_stringify(val)
          } else {
            throw error_NewError("Property `version` of package.yml expected to be a scalar")
          }
        }
      } elif nodeKey == "description" {
        if mappingNode.value != nil {
          val := mappingNode.value
          if val is YAML.NodeScalar {
            result.description = YAML.Node_stringify(val)
          } else {
            throw error_NewError("Property `description` of package.yml expected to be a scalar")
          }
        }
      } elif nodeKey == "license" {
        if mappingNode.value != nil {
          val := mappingNode.value
          if val is YAML.NodeScalar {
            result.license = YAML.Node_stringify(val)
          } else {
            throw error_NewError("Property `license` of package.yml expected to be a scalar")
          }
        }
      } elif nodeKey == "main" {
        if mappingNode.value != nil {
          val := mappingNode.value
          if val is YAML.NodeScalar {
            result.mainFile = YAML.Node_stringify(val)
          } else {
            throw error_NewError("Property `main` of package.yml expected to be a scalar")
          }
        }
      } elif nodeKey == "files" {
        if mappingNode.value != nil {
          val := mappingNode.value
          if val is YAML.NodeSeq {
            items := val.items
            loop i := 0; i < items.len; i++ {
              item: YAML.Node = items[i]
              if item is YAML.NodeScalar {
                file := YAML.Node_stringify(item)
                result.files.push(file)
              } else {
                throw error_NewError("Property `files` of package.yml expected to be a sequence of scalars")
              }
            }
          } else {
            throw error_NewError("Property `files` of package.yml expected to be a sequence")
          }
        }
      } elif nodeKey == "packages" {
        if mappingNode.value != nil {
          val := mappingNode.value
          if val is YAML.NodeMap {
            loop i := 0; i < val.mappings.len; i++ {
              mapping := val.mappings[i]
              packageName := YAML.Node_stringify(mapping.key)

              if mapping.value == nil {
                throw error_NewError("Property `packages[].version` of package.yml is required")
              } else {
                val2 := mapping.value
                if val2 is YAML.NodeScalar {
                  packageVersion := YAML.Node_stringify(val2)
                  result.packages.set(packageName, packageVersion)
                } else {
                  throw error_NewError("Property `packages[].version` of package.yml expected to be a scalar")
                }
              }
            }
          } else {
            throw error_NewError("Property `packages` of package.yml expected to be a map")
          }
        }
      } elif nodeKey == "scripts" {
        if mappingNode.value != nil {
          val := mappingNode.value
          if val is YAML.NodeMap {
            loop i := 0; i < val.mappings.len; i++ {
              mapping := val.mappings[i]
              scriptName := YAML.Node_stringify(mapping.key)

              if mapping.value == nil {
                throw error_NewError("Property `scripts[].value` of package.yml is required")
              } else {
                val2 := mapping.value
                if val2 is YAML.NodeScalar {
                  scriptValue := YAML.Node_stringify(val2)
                  result.scripts.set(scriptName, scriptValue)
                } else {
                  throw error_NewError("Property `scripts[].value` of package.yml expected to be a scalar")
                }
              }
            }
          } else {
            throw error_NewError("Property `scripts` of package.yml expected to be a map")
          }
        }
      }
    }
  }

  if result.name.empty {
    throw error_NewError("Property `name` of package.yml is required")
  } elif result.version.empty {
    throw error_NewError("Property `version` of package.yml is required")
  }

  return result
}
