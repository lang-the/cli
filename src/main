/*!
 * Copyright (c) Aaron Delasy
 * Licensed under the MIT License
 */

const VERSION_NUM := "0.12.1"
const VERSION_NAME := "Submarine Ocean"

main {
  mut args := Args{lineLength: 70}
  defaultOutput := os_NAME == "Windows" ? "a.exe" : "a.out"

  args
    .usage("the [options]")
    .usage("the <command> file [options]")

    .command(
      "build",
      "Build source directory into single file",
      default: "."
    )
    .command("compile", "Compile file")
    .command("install", "Add package to your project", alias: "i")
    .command("lex", "Lex file")
    .command("parse", "Parse file")
    .command("run", "Compile and execute file")
    .command("test", "Test source directory", default: ".")
    .command("update", "Update package")
    .command(
      "upgrade",
      "Self-upgrade CLI to newest version",
      hasValue: false
    )

    .option(
      "build-dir",
      "Build output directory, by default \"build\"",
      default: "build"
    )
    .option("compiler", "Offline compiler to use instead of cloud servers")
    .option("exclude", "Exclude paths separated by comma")
    .option("help", "Print help information", alias: "h", hasValue: false)
    .option(
      "output",
      "Executable output path, by default \"" + defaultOutput + "\"",
      alias: "o",
      default: defaultOutput
    )
    .option(
      "platform",
      "Target platform, one of: linux, macos, windows",
      default: os_NAME.lower
    )
    .option("run-args", "Arguments to be passed when running executable")
    .option(
      "the",
      "The Programming Language version, valid formats: latest, 1, 1.1, 1.1.1",
      default: "latest"
    )
    .option("version", "Print version", alias: "v", hasValue: false)

    .example("the -h")
    .example("the --version")
    .example("the build")
    .example("the compile /path/to/file")
    .example("the lex /path/to/file")
    .example("the compile /path/to/file --the=1.0")
    .example("the lex /path/to/file --the=1.0.0")
    .example("the run /path/to/file --platform=macos")
    .example("the update the/args")
    .example("the install the/error@1.0.0")
    .example("the upgrade")

  args.parse(process_args)

  mut action := ""
  mut buildDir := ""
  mut compiler := ""
  mut excludePaths := [".git", ".svn", "thumbs.db", ".DS_Store"]
  mut outputPath := ""
  mut packageArgument := ""
  mut platform := ""
  mut runArgs := ""
  mut targetPath := ""
  mut version := ""

  loop i := 0; i < args.data.len; i++ {
    arg := args.data[i]

    if (
      arg.command == "build" ||
      arg.command == "compile" ||
      arg.command == "lex" ||
      arg.command == "parse" ||
      arg.command == "test" ||
      arg.command == "run"
    ) {
      action = arg.command
      targetPath = arg.value
    } elif arg.command == "install" || arg.command == "update" {
      action = arg.command
      packageArgument = arg.value
    } elif arg.command == "upgrade" {
      action = arg.command
    } elif arg.option == "help" || arg.option == "version" {
      action = arg.option
    } elif arg.option == "build-dir" {
      buildDir = arg.value
    } elif arg.option == "compiler" {
      compiler = arg.value
    } elif arg.option == "exclude" {
      excludePaths.merge(arg.value.split(","))
    } elif arg.option == "output" {
      outputPath = fs_isAbsoluteSync(arg.value)
        ? arg.value
        : process_cwd() + path_SEP + arg.value
    } elif arg.option == "platform" {
      platform = arg.value
    } elif arg.option == "run-args" {
      runArgs = arg.value
    } elif arg.option == "the" {
      version = arg.value
    }
  }

  if action == "help" {
    print(args.help(), terminator: "")
    process_exit(0)
  } elif action == "version" {
    print("The Programming Language")
    print("Version", VERSION_NUM, "(" + VERSION_NAME + ")")
    print("Copyright (c) Aaron Delasy")
    process_exit(0)
  }

  qs := "?v=" + version + "&p=" + platform

  if action == "build" {
    Builder_init(targetPath, buildDir, excludePaths)
  } elif action == "compile" || action == "lex" || action == "parse" {
    command(compiler, action, targetPath, outputPath, qs)
  } elif action == "install" || action == "update" {
    mut packager := Packager_init()

    if action == "install" && packageArgument.empty {
      packager.installAll()
    } elif action == "install" {
      packager.install(packageArgument)
    } elif action == "update" && packageArgument.empty {
      packager.updateAll()
    } elif action == "update" {
      packager.update(packageArgument)
    }

    packager.deinit()
  } elif action == "run" {
    command(compiler, "compile", targetPath, outputPath, qs)

    cmd := outputPath + (runArgs.empty ? "" : " ") + runArgs
    stdout := process_runSync(cmd)

    fs_rmSync(outputPath)
    print(stdout.str(), terminator: "")
  } elif action == "test" {
    Tester_init(targetPath, excludePaths)
  } elif action == "upgrade" {
    executablePath := os_NAME == "Windows"
      ? "C:/Program Files/The/the.exe"
      : "/usr/local/bin/the"
    url := "https://cdn.thelang.io/cli-core-" + os_NAME.lower

    mut req := request_open("GET", url)
    res := req.read()
    req.close()

    fs_writeFileSync(executablePath, res.data)
    chmodAddSync(executablePath, 0o111)
  }
}
